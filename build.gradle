plugins {
    id "java"
    id "maven-publish"
    id "signing"
    alias(libs.plugins.gradle.embulk.plugins)
    id "checkstyle"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.11.1-SNAPSHOT"
description = "Loads records from local files."

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly libs.embulk.spi
    compileOnly libs.slf4j

    implementation libs.embulk.util.config
    implementation libs.validation
    implementation platform(libs.jackson.bom)
    implementation libs.bundles.jackson

    implementation libs.embulk.util.file

    testImplementation libs.junit4
    testImplementation libs.embulk.spi
    testImplementation libs.embulk.core
    testImplementation libs.embulk.deps
    testImplementation libs.embulk.junit4
}

embulkPlugin {
    mainClass = "org.embulk.input.file.FileGlobInputPlugin"
    category = "input"
    type = "file_glob"
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

tasks.withType(Checkstyle) {
    reports {
        // Not to skip up-to-date checkstyles.
        outputs.upToDateWhen { false }
    }
}

checkstyle {
    toolVersion = libs.versions.checkstyle.get()
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    configProperties = [
        "org.checkstyle.google.suppressionfilter.config": file("${rootProject.projectDir}/config/checkstyle/checkstyle-suppressions.xml"),
    ]
    ignoreFailures = false
    maxWarnings = 0
}

gem {
    authors = [
        "kaki"
    ]
    email = [
        "kakoni@gmail.com"
    ]
    summary = "azure file input plugin for Embulk"
    homepage = "https://github.com/kaki/embulk-input-file_glob"
    licenses = [ "Apache-2.0" ]
}

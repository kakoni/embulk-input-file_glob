plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.5.5" apply false
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.10.41-SNAPSHOT"

task releaseCheck {
    doFirst {
        if (rootProject.version.endsWith("-SNAPSHOT")) {
            throw new GradleException("Not for release. The version in build.gradle is SNAPSHOT: ${rootProject.version}")
        }
    }
    doLast {
        println "Ready. Run 'release' task."
    }
}

task release {
    dependsOn "releaseCheck"
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"

            exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
            showCauses = true
            showExceptions = true
            showStackTraces = true
            showStandardStreams = true

            outputs.upToDateWhen { false }
        }
    }

    afterEvaluate { project ->
        rootProject.check.dependsOn project.check
        rootProject.release.dependsOn project.publishMavenPublicationToMavenCentralRepository
    }
}

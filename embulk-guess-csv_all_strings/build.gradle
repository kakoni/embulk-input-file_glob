apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "checkstyle"
apply plugin: "org.embulk.embulk-plugins"

repositories {
    mavenCentral()
}

group = rootProject.group
version = rootProject.version
description = "Guesses a configuration with a schema if an input is CSV."

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.10.44"
    compileOnly "org.slf4j:slf4j-api:2.0.6"

    implementation project(":embulk-guess-csv")
    implementation "org.embulk:embulk-util-config:0.3.3"
    implementation "org.embulk:embulk-util-guess:0.3.1"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-spi:0.10.44"
    testImplementation "org.embulk:embulk-core:0.10.44"
    testImplementation "org.embulk:embulk-deps:0.10.44"
    testImplementation "org.embulk:embulk-junit4:0.10.44"
    testImplementation "com.google.guava:guava:18.0"

    testImplementation project(":embulk-formatter-csv")
    testImplementation project(":embulk-input-file")
    testImplementation project(":embulk-output-file")
    testImplementation project(":embulk-parser-csv")
    testImplementation project(":embulk-guess-bzip2")
    testImplementation project(":embulk-guess-gzip")
    testImplementation project(":embulk-guess-json")
}

embulkPlugin {
    mainClass = "org.embulk.guess.csv.CsvAllStringsGuessPlugin"
    category = "guess"
    type = "csv_all_strings"
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
        // No NOTICE because the Maven artifact does not contain embulk-util-guess by itself.
        // But, be noted when building a package that includes dependencies of embulk-guess-csv.
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
        // No NOTICE because the Maven artifact does not contain embulk-util-guess by itself.
        // But, be noted when building a package that includes dependencies of embulk-guess-csv.
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
        // No NOTICE because the Maven artifact does not contain embulk-util-guess by itself.
        // But, be noted when building a package that includes dependencies of embulk-guess-csv.
    }
}

publishing {
    publications {
         maven(MavenPublication) {
            afterEvaluate { project ->
                groupId = project.group
                artifactId = project.name
            }

            from components.java
             // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
             // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                afterEvaluate { project ->
                    name = project.name
                    description = project.description
                }
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@treasure-data.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-standards.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-standards.git"
                    url = "https://github.com/embulk/embulk-standards"
                }

                withXml {
                    if (asNode().getAt("dependencies").isEmpty()) {
                        asNode().appendNode("dependencies")
                    }
                    project.configurations.compileOnly.allDependencies.each { dependency ->
                        asNode().dependencies[0].appendNode("dependency").with {
                            it.appendNode("groupId", dependency.group)
                            it.appendNode("artifactId", dependency.name)
                            it.appendNode("version", dependency.version)
                            it.appendNode("scope", "provided")
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}

test {
    maxHeapSize = "1536m"
    forkEvery = 1  // test processes are forked by each test class (default is 0)
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

tasks.withType(Checkstyle) {
    reports {
        // Not to skip up-to-date checkstyles.
        outputs.upToDateWhen { false }
    }
}

checkstyle {
    toolVersion "8.7"
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    configProperties = [
        "checkstyle.config.path": file("${projectDir}/config/checkstyle")
    ]
    ignoreFailures = false
    maxWarnings = 0  // https://github.com/gradle/gradle/issues/881
}
